
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьКарту();
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПостроитьМаршрут(Команда) 
	
	ИнформацияОМаршрутеJSON = Элементы.ПолеHTML.Документ.defaultView.buildRoute();
	ПрочитатьИнформациюОМаршрутеИзJSON(ИнформацияОМаршрутеJSON);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьМаршрут(Команда)
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Метки);
	МеткиJSON = ЗаписьJSON.Закрыть();
	
	Название = "";
	Если ВвестиСтроку(Название, "Введите название маршрута") Тогда
		
	    ЗаписатьМаршрутВСправочник(МеткиJSON, Название);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НайтиМесто(Команда)
	
	Элементы.ПолеHTML.Документ.defaultView.findPlace(Поиск);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьМаршрут(Команда)

	Метки = Неопределено;
	Дистанция = 0;
	ВремяВПути = 0;
	НаборВысоты = 0;
	ПотеряВысоты = 0;
	ДистанцияUI = "";
	ВремяВПутиUI = "";
	НаборВысотыUI = "";
	ПотеряВысотыUI = "";
	
	Элементы.ПолеHTML.Документ.defaultView.clearRoute();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВGoogleКарты(Команда)
	
	КоличествоМеток = Метки.Количество();
	
	Ссылка = "https://www.google.com/maps/dir/?api=1&travelmode=walking";
	Ссылка = Ссылка + "&origin=" + СтрЗаменить(Строка(Метки[0][1]), ",", ".") + ", " + СтрЗаменить(Строка(Метки[0][0]), ",", ".");
	Ссылка = Ссылка + "&waypoints=";
	
	Для Индекс = 1 по (КоличествоМеток - 2) Цикл
		
		Ссылка = Ссылка + СтрЗаменить(Строка(Метки[Индекс][1]), ",", ".") + ", " + СтрЗаменить(Строка(Метки[Индекс][0]), ",", ".") + "|";
		
	КонецЦикла;
	
	Ссылка = Лев(Ссылка, СтрДлина(Ссылка) - 1);
	
	Ссылка = Ссылка + "&destination=" + СтрЗаменить(Строка(Метки[КоличествоМеток - 1][1]), ",", ".") + "," + СтрЗаменить(Строка(Метки[КоличествоМеток - 1][0]), ",", ".");
	
	ЗапуститьПриложение(Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции          

&НаСервере
Процедура ИнициализироватьКарту()
	
	Значение = РеквизитФормыВЗначение("Объект");
	ПолеHTML = ПолучитьСтрокуИзДвоичныхДанных(Значение.ПолучитьМакет("ШаблонКартыHTML"));	
		
КонецПроцедуры

&НаСервере
Процедура ЗаписатьМаршрутВСправочник(МеткиJSON, Название)
	
	НовыйЭлемент = Справочники.ЛокальныеМаршруты.СоздатьЭлемент();
	НовыйЭлемент.Наименование = Название;
	НовыйЭлемент.МеткиJSON = МеткиJSON;
	НовыйЭлемент.Дистанция = ДистанцияUI;
	НовыйЭлемент.ВремяВПути = ВремяВПутиUI;
	НовыйЭлемент.ВремяВПутиСек = ВремяВПути;
	НовыйЭлемент.НаборВысоты = НаборВысотыUI;
	НовыйЭлемент.ПотеряВысоты = ПотеряВысотыUI;
	НовыйЭлемент.Записать();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИнформациюОМаршрутеИзJSON(ИнформацияОМаршрутеJSON)

	ИнформацияОМаршруте = ПолучитьЗначениеИзJSON(ИнформацияОМаршрутеJSON);
	Метки = Новый ФиксированныйМассив(ИнформацияОМаршруте.markersCoords);
	Дистанция = ИнформацияОМаршруте.distance;
	ВремяВПути = ИнформацияОМаршруте.duration;   
	НаборВысоты = ИнформацияОМаршруте.elevationGain;
	ПотеряВысоты = ИнформацияОМаршруте.elevationLoss;
	ДистанцияUI = Строка(Дистанция) + " м";
	Секунды = ВремяВПути;
	ВремяВПутиUI = КонвертироватьСекунды(Секунды);
	НаборВысотыUI = Строка(НаборВысоты / 100) + " м";
	ПотеряВысотыUI = Строка(ПотеряВысоты / 100) + " м";
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеИзJSON(JSON)

	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(JSON);
	
	Возврат ПрочитатьJSON(ЧтениеJSON);
	
КонецФункции 

&НаСервереБезКонтекста
Функция КонвертироватьСекунды(Секунды)
	
	Результат = "";
	
	Если Секунды / 86400 >= 1 Тогда
		
		Результат = Результат + Строка(Цел(Секунды / 86400)) + " д ";
		Секунды = Секунды % 86400;
		
	КонецЕсли;
	
	Если Секунды / 3600 >= 1 Тогда
		
		Результат = Результат + Строка(Цел(Секунды / 3600)) + " ч ";
		Секунды = Секунды % 3600;
		
	КонецЕсли;
	
	Если Секунды / 60 >= 1 Тогда
		
		Результат = Результат + Строка(Цел(Секунды / 60)) + " м ";
		Секунды = Секунды % 60;
		
	КонецЕсли;
	
	Если Секунды = 0 Тогда
		
		Результат = Лев(Результат, СтрДлина(Результат) - 1);
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Результат + Строка(Секунды) + " с";
	
КонецФункции

#КонецОбласти